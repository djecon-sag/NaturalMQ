** Example 'PUTIBMMQ': Put message to IBM-MQ queue.
************************************************************************
DEFINE DATA LOCAL
1 MQ-QUEUE-VIEW VIEW OF MQ-QUEUE
  2 ERROR-CODE                /* MQ reason code
  2 ERROR-TEXT                /* Text describing the reason code
  2 FUNCTION                  /* Operation: GET, PUT
  2 QMANAGER                  /* Queue manager name
  2 QNAME                     /* Queue name
  2 PRIORITY                  /* Priority (0=low, 9=high)
  2 MESSAGE-10K               /* Extended message field
  2 PUT-DATE                  /* Put date (YYYYMMDD)
  2 PUT-TIME                  /* Put time (HHMMSSTH)
  2 REPLY-TO-QNAME            /* Queue name for receiving reply
  2 REPLY-TO-QMANAGER         /* Queue manager to receive reply
  2 USER-ID                   /* User ID of PUT/GET requester
*
1 EMPLOY-VIEW VIEW OF EMPLOYEES
  2 NAME
*
1 DATE-A      (A8)
1 TIME-A      (A8)
1 USER1       (A8)
1 Q-MANAGER   (A48)
1 Q-NAME      (A48)
END-DEFINE
*
* The PUT function is used to send (or write) a message to a queue.
* The message remains in the queue until it is either retrieved
* (using GET) or expires whichever occurs first.
*
SET KEY ALL
INPUT (AD=MITL'_' IP=OFF ZP=ON SG=OFF CD=TU)
  'PUT information on Natural Messaging Queue' (I) /
  '------------------------------------------' (I) //
  'Queue name ..............' Q-NAME         /
  'Queue manager name ......' Q-MANAGER      ///
  'Press ENTER to continue or any PF-key to stop.'
/*
IF *PF-KEY NE 'ENTR'
  ESCAPE ROUTINE
END-IF
*
* In the example below, EMPLOYEE-VIEW data will be written in MQ-QUEUE
*
READ EMPLOY-VIEW BY ISN STARTING FROM 1 ENDING AT 10
*
* Move current time, date and user to the queue
*
  MOVE EDITED *TIMX (EM=HHIISST'0') TO TIME-A
  MOVE *DATN TO DATE-A
  MOVE *USER TO USER1
*
* Move name of the employee to the queue
*
  MOVE NAME  TO MQ-QUEUE-VIEW.MESSAGE-10K
*
* Perform function PUT to write data into the queue
*
  PROCESS MQ-QUEUE-VIEW USING
    MQ-QUEUE-VIEW.FUNCTION             = 'PUT',
    MQ-QUEUE-VIEW.QMANAGER             = Q-MANAGER,
    MQ-QUEUE-VIEW.QNAME                = Q-NAME,
    MQ-QUEUE-VIEW.PRIORITY             = 1,
    MQ-QUEUE-VIEW.PUT-DATE             = DATE-A,
    MQ-QUEUE-VIEW.PUT-TIME             = TIME-A,
    MQ-QUEUE-VIEW.REPLY-TO-QMANAGER    = Q-MANAGER,
    MQ-QUEUE-VIEW.REPLY-TO-QNAME       = Q-NAME,
    MQ-QUEUE-VIEW.USER-ID              = USER1
*
  PRINT 'Message count        :  ' *COUNTER (AD=L)
  PRINT 'Message              :  ' MQ-QUEUE-VIEW.MESSAGE-10K
  PRINT 'Queue manager        :  ' MQ-QUEUE-VIEW.QMANAGER
  PRINT 'Queue name           :  ' MQ-QUEUE-VIEW.QNAME
  PRINT 'Priority             :  ' MQ-QUEUE-VIEW.PRIORITY (AD=L)
  PRINT 'Put date             :  ' MQ-QUEUE-VIEW.PUT-DATE
  PRINT 'Put time             :  ' MQ-QUEUE-VIEW.PUT-TIME
  PRINT 'User ID              :  ' MQ-QUEUE-VIEW.USER-ID
  PRINT 'Reply queue manager  :  ' MQ-QUEUE-VIEW.REPLY-TO-QMANAGER
  PRINT 'Reply queue name     :  ' MQ-QUEUE-VIEW.REPLY-TO-QNAME
  PRINT '*'(70)(I)
*
* Check for error
*
  IF ERROR-CODE NE 0
    PRINT / 'Error occurred while performing the PUT function.' /
    PRINT 'Error code          :  ' ERROR-CODE (AD=L)
    PRINT 'Error text          :  ' ERROR-TEXT
    PRINT '*'(70)(I)
    ESCAPE BOTTOM                  /* Stop processing if error occurs
  END-IF
*
END-READ
*
END
